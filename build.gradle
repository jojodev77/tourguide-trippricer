buildscript {
  dependencies {
    classpath "gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.22.1"
  }
}

plugins {
  id 'org.springframework.boot' version '2.6.6'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'java'
  id 'jacoco'
}

group = 'RewardsCentral '
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'



repositories {
  mavenCentral()
  flatDir {
    dirs 'libs'
  }
}

ext {
  set('springCloudVersion', "2021.0.1")
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation group: 'org.javamoney', name: 'moneta', version: '1.3'
  implementation group: 'com.jsoniter', name: 'jsoniter', version: '0.9.23'
  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
  implementation('org.springframework.boot:spring-boot-starter-jdbc')
  runtimeOnly('com.h2database:h2')
  testImplementation 'org.springframework.boot:spring-boot-starter-test'

}

// docker: plugin apply
apply plugin: 'com.palantir.docker'

// docker: new task docker
docker {
  dependsOn build
  name "${project.name}"
  files bootJar.archivePath
  buildArgs(['JAR_FILE': "${bootJar.archiveName}"])
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}
jacoco {
  toolVersion = "0.8.7"
}

jacocoTestReport {
  reports {
    html.required = true
  }
  executionData.from fileTree(buildDir).include("/jacoco/*.exec")
}


task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
  group 'verification'
  description = 'Generates an aggregate report from all subprojects'
  dependsOn(subprojects.test)

  additionalSourceDirs.from = files(subprojects.sourceSets.main.allSource.srcDirs)
  sourceDirectories.from = files(subprojects.sourceSets.main.allSource.srcDirs)
  classDirectories.from = files(subprojects.sourceSets.main.output)
  executionData.from = files(subprojects.jacocoTestReport.executionData)

  reports {
    xml.required = true
    html.required = true
  }
}



tasks.named('test') {
  useJUnitPlatform()
}
